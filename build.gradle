plugins {
    id 'idea'
    id 'groovy'
    id 'com.gradle.plugin-publish' version '1.2.0'
    id 'com.github.hierynomus.license' version '0.16.1'
}

group = 'com.github.jk1'
version = '2.8'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()

    testImplementation gradleTestKit()
    testImplementation('org.spockframework:spock-core:2.4-M4-groovy-3.0') {
        exclude group: 'org.codehaus.groovy'
    }
}

idea {
    module {
        downloadSources = true
        outputDir file("$buildDir/idea/main")
        testOutputDir file("$buildDir/idea/test")
    }
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
    systemProperty("spock.snapshots.rootPath", "src/test/resources")
    systemProperty("spock.snapshots.updateSnapshots", !System.getenv("CI"))
    systemProperty("spock.snapshots.writeActualSnapshotOnMismatch", !System.getenv("CI"))

    outputs.upToDateWhen { false }
}

// local maven publishing to ease development
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.github.jk1.dependency-license-report'
            artifactId 'com.github.jk1.dependency-license-report.gradle.plugin'
            from components.java
        }
    }
}

// release publishing to gradle plugin repo
gradlePlugin {
    website = 'https://github.com/jk1/Gradle-License-Report'
    vcsUrl = 'https://github.com/jk1/Gradle-License-Report'
    plugins {
        licenseReportPlugin {
            id = 'com.github.jk1.dependency-license-report'
            displayName = 'Gradle dependency license report plugin'
            description = 'A plugin for generating reports about the licenses of the dependencies for your Gradle project'
            tags.set(['dependency-management', 'license', 'reporting'])
            implementationClass = 'com.github.jk1.license.LicenseReportPlugin'
        }
    }
}
