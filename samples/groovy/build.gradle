import com.github.jk1.license.render.InventoryHtmlReportRenderer
import com.github.jk1.license.importer.XmlReportImporter
import com.github.jk1.license.filter.LicenseBundleNormalizer

buildscript {
    repositories {
        mavenLocal()
    }
    dependencies {
        classpath 'com.github.jk1.dependency-license-report:com.github.jk1.dependency-license-report.gradle.plugin:2.+'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.jk1.dependency-license-report'

repositories { mavenCentral() }

dependencies {
    implementation 'org.springframework:spring-tx:3.2.3.RELEASE'
    implementation 'com.sun.mail:javax.mail:1.5.4'
    implementation 'org.ehcache:ehcache:3.3.1'
    implementation 'org.apache.geronimo.specs:geronimo-jta_1.0.1B_spec:1.0.1'
}

licenseReport {
    renderers = [new InventoryHtmlReportRenderer('report.html', 'Backend')]
    filters = [new LicenseBundleNormalizer()]
    importers = [new XmlReportImporter('Front End', new File(project.projectDir, 'externalDependencies.xml'))]
    configurations = ALL
}

task printDependencies {
    doLast {
        configurations.compile.allDependencies*.each { Dependency dep ->
            println "${dep.getClass().simpleName}[${dep.getClass().interfaces*.simpleName.join(', ')}] group[$dep.group] name[$dep.name] $dep"
        }
    }
}

task printResolvedFiles {
    doLast {
        def resolved = configurations.compile.resolvedConfiguration
        resolved.firstLevelModuleDependencies.each { ResolvedDependency resolvedDep ->
            resolvedDep.moduleArtifacts.each { println "$it.name - $it.classifier - $it.type: $it.file" }
        }
    }
}
