import com.github.jk1.license.render.InventoryHtmlReportRenderer
import com.github.jk1.license.importer.XmlReportImporter
import com.github.jk1.license.filter.LicenseBundleNormalizer

plugins {
    id 'com.github.jk1.dependency-license-report' version '3.1.1'  // x-release-please-version
    id 'groovy'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework:spring-tx:3.2.3.RELEASE'
    implementation 'com.sun.mail:javax.mail:1.5.4'
    implementation 'org.ehcache:ehcache:3.3.1'
    implementation 'org.apache.geronimo.specs:geronimo-jta_1.0.1B_spec:1.0.1'
}

licenseReport {
    renderers = [new InventoryHtmlReportRenderer('report.html', 'Backend')]
    filters = [new LicenseBundleNormalizer()]
    importers = [new XmlReportImporter('Front End', layout.projectDirectory.file('../configs/externalDependencies.xml').asFile)]

    allowedLicensesFile = layout.projectDirectory.file('../configs/allow-mit-sample.json')
}

tasks.register("printDependencies") {
    doLast {
        configurations.implementation.allDependencies*.each { Dependency dep ->
            println "${dep.class.simpleName}[${dep.class.interfaces*.simpleName.join(', ')}] group[$dep.group] name[$dep.name] $dep"
        }
    }
}

tasks.register("printResolvedFiles") {
    doLast {
        configurations.runtimeClasspath
                .resolvedConfiguration
                .resolvedArtifacts
                .each { println "$it.name - $it.classifier - $it.type: $it.file" }
    }
}
